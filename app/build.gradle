apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }

    lintOptions {
        abortOnError false
    }
}

task runAppCheck {
    dependsOn check
}

task runAppAppengineStart {
    evaluationDependsOn ":jokecloud"
    dependsOn ":jokecloud:appengineRun"
    mustRunAfter runAppCheck

    project(":jokecloud"){
        appengine{
            daemon true
        }
    }
}

task runAppConnectedCheck {
    mustRunAfter runAppAppengineStart
    dependsOn = [
            connectedCheck,
            runAppAppengineStart
    ]
}

task runAppAppengineStop {
    dependsOn ":jokecloud:appengineStop"
    mustRunAfter runAppConnectedCheck
}

task runAppEndToEndTests {
    dependsOn = [
            runAppCheck,
            runAppConnectedCheck,
            runAppAppengineStart,
            runAppAppengineStop,
    ]
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Added for AdMob
    compile project(':jokeme')
    compile project(':jokemachine')
//    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:appcompat-v7:22.2.1'
//    compile 'com.google.android.gms:play-services:8.1.0'
    compile 'com.google.android.gms:play-services:7.3.0'
    compile project(path: ':jokecloud', configuration: 'android-endpoints')
    testCompile 'junit:junit:4.12'
}
